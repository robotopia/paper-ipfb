reset

dat = "cartoon.dat"

myfont = ",28"

unset key
unset tics

# The PFB parameters. These should match the parameters used in cartoon.m
N = 1536
ntaps = 4
tapsize = N/ntaps

set multiplot

lgap = 0.02 # Left margin gap in screen units
rgap = 0.02 # Right margin gap
hgap = 0.04 # Gap between panels

w = (1.0 - (lgap + rgap + (ntaps-1)*hgap)) / ntaps # Width of one plot
W = w + hgap                                       # Width of one plot plus one gap

tgap = 0.02
h    = 0.16
vgap = 0.08
H    = h + vgap

L(t) = lgap + W*t
R(t) = L(t) + w
T(t) = (t <= 2 ? 1.0 - (tgap + H*t) : tgap + h)
B(t) = (t <= 2 ? T(t) - h           : tgap)

lastL(t) = (t == 1 ? 0.2 : 0.8 - w)
lastR(t) = lastL(t) + w


do for [t=0:ntaps-1] {

    set lmargin at screen L(t)
    set rmargin at screen R(t)

    set samples tapsize

    set xrange [t*tapsize:(t+1)*tapsize-1]

    # Plot the signal panels

    set tmargin at screen T(0)
    set bmargin at screen B(0)

    labelv = (B(0)+T(1))/2.0+0.005
    midh   = (L(t)+R(t))/2.0

    set label t+1 "⊗"     at screen midh, screen labelv center font myfont
    set arrow t+1       from screen midh, screen B(0) to screen midh, screen labelv+0.01 nohead lw 5
    set arrow t+ntaps+1 from screen midh, screen T(1) to screen midh, screen labelv-0.015 nohead lw 5

    set yrange [-4:4]
    plot dat index 0 using 0:1 w l

    # Plot the filter panels

    set tmargin at screen T(1)
    set bmargin at screen B(1)

    labelv = (B(1)+T(2))/2.0+0.005

    set label t+1 "⊜"     at screen midh, screen labelv center font myfont
    set arrow t+1       from screen midh, screen B(1) to screen midh, screen labelv+0.01 nohead lw 5
    set arrow t+ntaps+1 from screen midh, screen T(2) to screen midh, screen labelv-0.015 nohead lw 5

    set yrange [-0.25:1.1]
    plot dat index 0 using 0:2 w l

    unset label

    # Plot the signal-times-filter panels

    set tmargin at screen T(2)
    set bmargin at screen B(2)

    labelv = (B(2)+T(3))/2.0
    unset arrow
    set arrow t+1 from screen midh, screen B(2) to screen (lastL(1) + lastR(1))/2.0, screen labelv+0.01 nohead lw 5

    set yrange [-1.1:1.6]
    plot dat index 0 using 0:3 w l

    unset arrow
}

# Plot the summed panel

set tmargin at screen T(3)
set bmargin at screen B(3)
set lmargin at screen lastL(1)
set rmargin at screen lastR(1)

set xrange [0:tapsize-1]

set label 1 "⊕" at screen (lastL(1) + lastR(1))/2.0, screen (B(2) + T(3))/2.0 center font myfont
set arrow 1 from screen (lastL(1) + lastR(1))/2.0, screen labelv-0.015 to screen (lastL(1) + lastR(1))/2.0, screen T(3) lw 5

plot dat index 1 using 0:1 w l

# Plot the FFT panel

set lmargin at screen lastL(2)
set rmargin at screen lastR(2)

set yrange [0:*]

set label 1 "FFT" at screen (lastR(1) + lastL(2))/2.0, screen (B(3) + T(3))/2.0+0.05 center font myfont
set arrow 1 from screen lastR(1), (B(3) + T(3))/2.0 \
            to   screen lastL(2), (B(3) + T(3))/2.0

plot dat index 1 using 0:2 w l

unset multiplot
