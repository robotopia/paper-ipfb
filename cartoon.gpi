reset

myfont = ",28"

N = 1536
K = 128.0

sinc(n)    = (n == N/2-1 ? 1 : sin(pi*(n+1-N/2)/K)/(pi*(n+1-N/2)/K))
hanning(n) = sin(pi*(n+1)/(N+1.0))**2
filter(n)  = sinc(n) * hanning(n)

randn(n) = (rand(0) + rand(0) + rand(0) + rand(0) + rand(0) + rand(0) + \
            rand(0) + rand(0) + rand(0) + rand(0) + rand(0) + rand(0)) - 6.0

signal(n) = cos(0.01*n) + randn(n)/2.0

unset key
unset tics

# The four "taps"
ntaps = 4
tapsize = N/ntaps

set samples N

set print $sig
do for [n=0:N-1] { print n, signal(n) }

set print $fil
do for [n=0:N-1] { print n, filter(n) }

set print $filsig
do for [n=0:N-1] { print n, filter(n)*signal(n) }

set samples tapsize
set print $summed
do for [n=0:tapsize-1] {
    s = 0
    do for [t=0:ntaps-1] { s = s + filter(t*tapsize + n)*signal(t*tapsize + n) }
    print n, s
}

set print

set multiplot

lgap = 0.02 # Left margin gap in screen units
rgap = 0.02 # Right margin gap
hgap = 0.04 # Gap between panels

w = (1.0 - (lgap + rgap + (ntaps-1)*hgap)) / ntaps # Width of one plot
W = w + hgap                                       # Width of one plot plus one gap

tgap = 0.02
h    = 0.16
vgap = 0.08
H    = h + vgap

L(t) = lgap + W*t
R(t) = L(t) + w
T(t) = (t <= 2 ? 1.0 - (tgap + H*t) : tgap + h)
B(t) = (t <= 2 ? T(t) - h           : tgap)

lastL(t) = (t == 1 ? 0.2 : 0.8 - w)
lastR(t) = lastL(t) + w


do for [t=0:ntaps-1] {

    set lmargin at screen L(t)
    set rmargin at screen R(t)

    set samples tapsize

    set xrange [t*tapsize:(t+1)*tapsize-1]

    # Plot the signal panels

    set tmargin at screen T(0)
    set bmargin at screen B(0)

    set label t+1 "⊗" at screen (L(t)+R(t))/2.0, screen (B(0)+T(1))/2.0+0.005 center font myfont

    set yrange [-4:4]
    plot $sig w l

    # Plot the filter panels

    set tmargin at screen T(1)
    set bmargin at screen B(1)

    set label t+1 "⊜" at screen (L(t)+R(t))/2.0, screen (B(1)+T(2))/2.0+0.005 center font myfont

    set yrange [-0.25:1.1]
    plot $fil w l

    unset label

    # Plot the signal-times-filter panels

    set tmargin at screen T(2)
    set bmargin at screen B(2)

    set yrange [-1.1:1.6]
    plot $filsig w l
}

# Plot the summed panel

set tmargin at screen T(3)
set bmargin at screen B(3)
set lmargin at screen lastL(1)
set rmargin at screen lastR(1)

set xrange [0:tapsize-1]

set label 1 "⊕" at screen (lastL(1) + lastR(1))/2.0, screen (B(2) + T(3))/2.0 center font myfont

plot $summed w l

set lmargin at screen lastL(2)
set rmargin at screen lastR(2)

set yrange [0:*]

set label 1 "FFT" at screen (lastR(1) + lastL(2))/2.0, screen (B(3) + T(3))/2.0+0.05 center font myfont
set arrow 1 from screen lastR(1), (B(3) + T(3))/2.0 \
            to   screen lastL(2), (B(3) + T(3))/2.0

plot 'ffted.txt' w l

unset multiplot
