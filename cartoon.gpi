reset

N = 1536
K = 128.0

sinc(n)    = (n == N/2-1 ? 1 : sin(pi*(n+1-N/2)/K)/(pi*(n+1-N/2)/K))
hanning(n) = sin(pi*(n+1)/(N+1.0))**2
filter(n)  = sinc(n) * hanning(n)

randn(n) = (rand(0) + rand(0) + rand(0) + rand(0) + rand(0) + rand(0) + \
            rand(0) + rand(0) + rand(0) + rand(0) + rand(0) + rand(0)) - 6.0

signal(n) = cos(0.01*n) + randn(n)/2.0

unset key
unset tics

set samples N

set print $sig
do for [n=0:N-1] { print n, signal(n) }

set print $fil
do for [n=0:N-1] { print n, filter(n) }

set print

set multiplot

# The four "taps"
ntaps = 4
tapsize = N/ntaps

lgap = 0.02 # Left margin gap in screen units
rgap = 0.02 # Right margin gap
mgap = 0.08 # Gap between panels

w = (1.0 - (lgap + rgap + (ntaps-1)*mgap)) / ntaps # Width of one plot
W = w + mgap                                       # Width of one plot plus one gap

do for [t=0:ntaps-1] {
    L = lgap + W*t
    R = L + w

    set lmargin at screen L
    set rmargin at screen R

    set samples N/ntaps

    set xrange [t*N/ntaps:(t+1)*N/ntaps-1]

    set tmargin at screen 0.98
    set bmargin at screen 0.78

    set label t+1 "âŠ—" at screen (L+R)/2.0, screen 0.74 center font ",16"

    set yrange [-4:4]
    plot $sig w l

    set tmargin at screen 0.70
    set bmargin at screen 0.50

    unset label

    set yrange [-0.25:1.1]
    plot $fil w l

}

unset multiplot
